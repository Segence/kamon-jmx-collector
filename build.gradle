plugins {
  id 'com.github.maiflai.scalatest' version '0.19'
  id 'nebula.integtest' version '3.4.0'
  id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.maiflai.scalatest'
apply plugin: 'nebula.integtest'
apply plugin: 'com.jfrog.bintray'

group = 'io.kamon'

repositories {
  mavenCentral()
  maven {
    url  'https://dl.bintray.com/segence/maven-oss-releases'
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['ProjectPublication']
  pkg {
    repo = 'maven-oss-releases'
    name = 'kamon-jmx-collector'
    userOrg = 'segence'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/segence/kamon-jmx-collector.git'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = gradleWrapperVersion
}

task printVersion {
  doLast {
    println project.version
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

dependencies {
  compileOnly(
    "org.scala-lang:scala-library:${scalaVersion}",
    "com.typesafe:config:${typesafeConfigVersion}",
    "io.kamon:kamon-core_${scalaMajorMinorVersion}:${kamonVersion}",
    "com.typesafe.akka:akka-actor_${scalaMajorMinorVersion}:${akkaVersion}"
  )
  compile(
    "com.segence.commons:jmx-collector:${jmxCollectorVersion}"
  )
  testCompile (
    "org.scalatest:scalatest_${scalaMajorMinorVersion}:${scalaTestVersion}",
    "org.mockito:mockito-core:${mockitoVersion}",
    "org.scala-lang:scala-library:${scalaVersion}",
    "com.typesafe:config:${typesafeConfigVersion}",
    "io.kamon:kamon-core_${scalaMajorMinorVersion}:${kamonVersion}",
    "com.typesafe.akka:akka-actor_${scalaMajorMinorVersion}:${akkaVersion}",
    "com.typesafe.akka:akka-testkit_${scalaMajorMinorVersion}:${akkaVersion}",
    "io.kamon:kamon-prometheus_${scalaMajorMinorVersion}:1.0.0",
    "org.apache.kafka:kafka_${scalaMajorMinorVersion}:1.1.0",
    "org.slf4j:slf4j-api:1.7.25",
    "org.slf4j:slf4j-simple:1.7.25"
  )
  testRuntime "org.pegdown:pegdown:${pegdownVersion}"
}

publishing {
  publications {
    ProjectPublication(MavenPublication) {
      artifactId "${artifactId}_${scalaMajorMinorVersion}"

      from components.java

      artifact sourceJar {
        classifier "sources"
      }
    }
  }
}
